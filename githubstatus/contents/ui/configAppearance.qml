import QtQuick
import QtQuick.Controls as QQC2
import QtQuick.Layouts
import org.kde.kirigami as Kirigami
import org.kde.kcmutils as KCM
import org.kde.kquickcontrols as KQuickControls

KCM.SimpleKCM {
    id: page

    property string cfg_badgeStyle
    property string cfg_badgeStyleDefault
    property color cfg_colorMajorOutage
    property color cfg_colorMajorOutageDefault
    property color cfg_colorPartialOutage
    property color cfg_colorPartialOutageDefault
    property color cfg_colorDegraded
    property color cfg_colorDegradedDefault
    property color cfg_colorMaintenance
    property color cfg_colorMaintenanceDefault
    property color cfg_colorOperational
    property color cfg_colorOperationalDefault
    property color cfg_colorCountBackground
    property color cfg_colorCountBackgroundDefault

    // One row per colour setting: the config key (without "cfg_") and its label.
    readonly property var colorSettings: [
        { key: "colorMajorOutage", label: i18n("Major outage:") },
        { key: "colorPartialOutage", label: i18n("Partial outage:") },
        { key: "colorDegraded", label: i18n("Degraded performance:") },
        { key: "colorMaintenance", label: i18n("Under maintenance:") },
        { key: "colorOperational", label: i18n("Operational:") },
        { key: "colorCountBackground", label: i18n("Count background:") }
    ]

    Kirigami.FormLayout {
        QQC2.ComboBox {
            readonly property var styles: ["square", "rounded", "dot"]
            Kirigami.FormData.label: i18n("Badge style:")
            model: [i18n("Square"), i18n("Rounded"), i18n("Icon with status dot")]
            currentIndex: Math.max(styles.indexOf(page.cfg_badgeStyle), 0)
            onActivated: index => page.cfg_badgeStyle = styles[index]
        }

        Item {
            Kirigami.FormData.isSection: true
            Kirigami.FormData.label: i18n("Colours")
        }

        Repeater {
            model: page.colorSettings

            RowLayout {
                required property var modelData
                readonly property string key: "cfg_" + modelData.key
                readonly property bool isDefault: Qt.colorEqual(page[key], page[key + "Default"])

                Kirigami.FormData.label: modelData.label

                KQuickControls.ColorButton {
                    color: page[key]
                    dialogTitle: modelData.label
                    showAlphaChannel: false
                    onAccepted: picked => page[key] = picked
                }

                QQC2.ToolButton {
                    icon.name: "edit-reset"
                    display: QQC2.AbstractButton.IconOnly
                    text: i18n("Reset to default")
                    enabled: !isDefault
                    onClicked: page[key] = page[key + "Default"]
                    QQC2.ToolTip.text: text
                    QQC2.ToolTip.visible: hovered
                }
            }
        }

        QQC2.Label {
            Layout.fillWidth: true
            wrapMode: Text.WordWrap
            font: Kirigami.Theme.smallFont
            opacity: 0.7
            text: i18n("The colours are also used for the status dot and the component list. The symbol and number on each badge switch between white and dark text to stay readable on the colour you pick.")
        }
    }
}
